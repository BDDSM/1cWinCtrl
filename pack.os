Перем ЗаписьXML, ЗаписьZIP, КаталогПакета;

Процедура ДобавитьЭлемент(Система, Архитектура, ИмяФайла)

	ЗаписьXML.ЗаписатьНачалоЭлемента("component");
	ЗаписьXML.ЗаписатьАтрибут("type", "native");
	ЗаписьXML.ЗаписатьАтрибут("os", Система);
	ЗаписьXML.ЗаписатьАтрибут("arch", Архитектура);
	ЗаписьXML.ЗаписатьАтрибут("path", ИмяФайла);
	ЗаписьXML.ЗаписатьКонецЭлемента();

	ЗаписьZIP.Добавить(КаталогПакета + ИмяФайла, РежимСохраненияПутейZIP.НеСохранятьПути);
	
КонецПроцедуры

Процедура MakePackage()

	КаталогСкрипта = ТекущийСценарий().Каталог;	
	КаталогПакета = КаталогСкрипта + "/bin/";

	ИмяФайла = КаталогПакета + "MANIFEST.XML";
	ЗаписьZIP = Новый ЗаписьZipФайла(КаталогСкрипта + "/AddIn.zip"); 

	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8", Ложь);
	ЗаписьXML.ЗаписатьБезОбработки("<?xml version=""1.0"" encoding=""UTF-8""?>");	
	ЗаписьXML.ЗаписатьНачалоЭлемента("bundle");	
	ЗаписьXML.ЗаписатьАтрибут("xmlns", "http://v8.1c.ru/8.2/addin/bundle");
	
	ДобавитьЭлемент("Windows" , "i386"   , "lib1cWinCtrlWin32.dll");
	ДобавитьЭлемент("Windows" , "x86_64" , "lib1cWinCtrlWin64.dll");
	ДобавитьЭлемент("Linux"   , "i386"   , "lib1cWinCtrlLin32.so");
	ДобавитьЭлемент("Linux"   , "x86_64" , "lib1cWinCtrlLin64.so");
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
	
	ЗаписьZIP.Добавить(ИмяФайла, РежимСохраненияПутейZIP.НеСохранятьПути);
	ЗаписьZIP.Записать();

КонецПроцедуры

MakePackage();